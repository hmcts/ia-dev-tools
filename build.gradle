group 'uk.gov.hmcts.reform'
version '0.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'jacoco'

repositories {

    maven {
        url "https://dl.bintray.com/hmcts/hmcts-maven"
    }

    jcenter()
}

dependencies {
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    compile group: 'com.google.guava', name: 'guava', version: '27.0-jre'
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: '4.1.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.9'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.9'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
}

checkstyle {
    maxWarnings = 0
    toolVersion = '8.10.1'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
    toolVersion = '6.12.0'
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSets = [
        'category/java/errorprone.xml',
        'category/java/bestpractices.xml'
    ]
}

jacoco {
    toolVersion = "0.8.1"
}

jacocoTestReport {
    executionData(test)
    reports {
        xml.enabled = true
        csv.enabled = false
        xml.destination = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

task ccdToJson(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    main = 'uk.gov.hmcts.reform.iadevtools.ccd.CcdDefinitionConverter'
}

task ccdToCompare(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    main = 'uk.gov.hmcts.reform.iadevtools.ccd.CcdDefinitionComparator'
}
